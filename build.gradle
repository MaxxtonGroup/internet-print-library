apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group 'com.maxxton'
version '1.3'

sourceCompatibility = '1.8'

if (!hasProperty('mainClass')) {
    ext.mainClass = 'PrintExample'
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.10'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
//            repository(url: mavenLocal().url)

            pom.project {
                name 'Internet Printer Library'
                packaging 'jar'
                description 'Java library for controlling printers over IP.'
                url 'https://github.com/MaxxtonGroup/internet-printer-library.git'

                scm {
                    url 'scm:git@github.com:MaxxtonGroup/internet-printer-library.git'
                    connection 'scm:git@github.com:MaxxtonGroup/internet-printer-library.git'
                    developerConnection 'scm:git@github.com:MaxxtonGroup/internet-printer-library.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 's.hermans'
                        name 'Steven Hermans'
                    }
                }
            }
        }
    }
}